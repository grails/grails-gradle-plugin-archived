apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'org.grails'
version = '2.0.0-SNAPSHOT'
archivesBaseName = "grails-gradle-plugin"

sourceCompatibility = "1.6"

repositories {
    maven { url "http://repo.grails.org/grails/libs-snapshots-local" }
    maven { url "http://repo.grails.org/grails/repo" }
}

dependencies {
    compile gradleApi()
    groovy localGroovy()
    
    compile "org.grails:grails-launcher:1.1-SNAPSHOT"
    testCompile "org.spockframework:spock-core:0.7-groovy-1.8", {
        exclude module: "groovy-all"
    }
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

apply from: "gradle/publish.gradle"
apply from: "gradle/idea.gradle"

task wrapper(type: Wrapper) {
    gradleVersion "1.6"
}